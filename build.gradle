plugins {
    id 'io.freefair.lombok' version '4.1.6' apply false
    id 'org.springframework.boot' version '2.2.5.RELEASE' apply false
    id 'com.jfrog.bintray' version '1.8.4' apply false
}

ext {
    javaProjects = [ project(':users-api'), project(':users-client'), project(':users-service') ]
    uploadProjects = [ project(':users-api'), project(':users-client') ]
}

allprojects {
    repositories {
        jcenter()
        maven { url 'https://repo.spring.io/milestone' }
    }
    group = 'net.plshark.users'
    version = '0.1.0'
}

configure(javaProjects) { project ->
    apply plugin: 'groovy'
    apply plugin: 'jacoco'
    apply plugin: 'io.freefair.lombok'

    generateLombokConfig.enabled = false

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        withSourcesJar()
    }

    compileJava.options.compilerArgs.add '-parameters'
    compileTestJava.options.compilerArgs.add '-parameters'

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled false
            csv.enabled false
        }
        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        '**/WebSecurityConfig.class'
                ])
            }))
        }
    }
}

configure(uploadProjects) { project ->
    apply plugin: 'maven-publish'

    publishing {
        repositories {
            maven {
                name = 'bintray'
                def bintrayUsername = 'twinklehawk'
                def bintrayRepoName = 'maven'
                def bintrayPackageName = 'net.plshark.users'
                url = "https://api.bintray.com/maven/$bintrayUsername/$bintrayRepoName/$bintrayPackageName/;publish=1"
                credentials {
                    username = System.getenv('BINTRAY_USER')
                    password = System.getenv('BINTRAY_API_KEY')
                }
            }
        }
    }
}
configure(uploadProjects - project(':platform')) { project ->
    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
    }
}
